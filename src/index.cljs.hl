; vim: set ft=clojure ts=2 sw=2:
(page "index.html"
  (:require [clojure.string :refer [blank? lower-case split]]))

(defn- make-rot13 [start]
  (->> (for [from (range 26),
             :let [to (mod (+ 13 from) 26)]]
         [(char (+ start from)) (char (+ start to))])
       (into {})))

(let [table (merge (make-rot13 97) (make-rot13 65))]
  (defn rot13 [s]
    (get table s s)))

(defc in "")
(defc permalink "")
(defc mailto-link "")
(defc tweet-link "")
(defc app-state :awaiting-submit) ; :awaiting-submit -> :encrypting -> :submitted
(defc can-copy? false)
(defc debug? false)

(def ^:const cp-button-id "cp")

; TODO: how to remove warnings like "Use of undeclared Var ... $input-el"
(def input-el (textarea :placeholder "Put your secret in the box"
                        :autofocus 1 :required 1 :value in :id "in"
                        :disabled (cell= (= app-state :encrypting))
                        :input #(do (reset! in @%)
                                    (reset! app-state :awaiting-submit)
                                    (.autosize (js/jQuery (.-target %))))))

(def $input-el (js/jQuery input-el))

(defn jq-sel-id [id]
  (js/jQuery (str "#" id)))

(defn get-by-id [id]
  (.get (jq-sel-id id)))

(def ^:const permalink-popover-id "plpo")
(def ^:const permalink-url-input-id "plui")
(def ^:const permalink-cp-button-id "plcb")
(def permalink-cp-inited false)

(defn permalink-popover-content []
  (div
    (input :type "url" :id permalink-url-input-id :readonly 1 :value (cell= permalink))
      (a :class "btn btn-xs" :id permalink-cp-button-id :toggle can-copy?
         :data-clipboard-text (cell= permalink)
         (span :class "octicon octicon-clippy"))))

;; query parameters
(def ^:const qp-in "i")
(def ^:const qp-debug "debug")

(def encrypt (atom nil))

(defn process-qp-in [value]
  (reset! in (js/decodeURIComponent value))
  (@encrypt))

(defn process-qp-debug [value]
  (reset! debug? (contains? #{"t" "true" "1" ""} value)))

(def ^:const qp-processors {qp-in process-qp-in qp-debug process-qp-debug})

; TODO: use http://docs.closure-library.googlecode.com/git/class_goog_Uri_QueryData.html ?
(defn process-query-str []
  (dorun
    (for [param-str (-> js/window .-location .-search (subs 1) (split #"&")),
          :let [paramkv (split param-str #"=")
                paramk (first paramkv)
                paramv (if (= 2 (count paramkv)) (last paramkv) "")
                processor (qp-processors paramk)]]
      (if processor (processor paramv)))))

(defn get-permalink []
  (str (.-protocol js/location) "//" (.-host js/location) (.-pathname js/location)
       "?" qp-in "=" (js/encodeURIComponent @in)))

(defn get-mailto-link []
  (str "mailto:?subject=secret%20message%20(shhh)&body="
       (js/encodeURIComponent (str "secret message:\n" @in "\n\ndecode at:\n" (get-permalink)))))

(defn get-tweet-link []
  (let [max-length 105
        hashtag "rot13"
        msg (str (if (> (count @in) max-length)
              (str (subs @in 0 max-length) "...")
              @in) (if hashtag (str " #" hashtag)))
        uri-encoded (js/encodeURIComponent msg)]
    (str "https://twitter.com/intent/tweet?"
         "text=" uri-encoded
         "&url="  (js/encodeURIComponent (get-permalink)))))

(reset! encrypt
  (fn []
    (reset! permalink (get-permalink))
    (reset! app-state :encrypting)
    (reset! in (apply str (map rot13 @in)))
    (reset! app-state :submitted)
    (reset! tweet-link (get-tweet-link))
    (reset! mailto-link (get-mailto-link))))

; TODO: rm setTimeout hack
(defn init-widgets []
  (js/setTimeout
    (fn []
      (do

        (.autosize $input-el)

        ; init ZeroClipboard client for main copy button
        (when-not (.isFlashUnusable js/ZeroClipboard)
          (reset! can-copy? true)
          (js/ZeroClipboard. (get-by-id cp-button-id)))


        ; hook up permalink popover and its copy button
        (let [po-el (jq-sel-id permalink-popover-id)]
          (.popover po-el
            (js-obj
              "container" "body"
              "content" permalink-popover-content
              "html" true
              "placement" "bottom"
              ))
          (.on po-el "shown.bs.popover"
            (fn []
              (.log js/console "show")
              (.select (jq-sel-id permalink-url-input-id))
              (if (and (not permalink-cp-inited) @can-copy?)
                (let [permalink-cp-button (get-by-id permalink-cp-button-id)]
                  (.log js/console permalink-cp-button)
                  (js/ZeroClipboard. permalink-cp-button)
                  (set! permalink-cp-inited true))))))))

   500))

(with-init!
  (do (process-query-str)
      (init-widgets)))

(.addEventListener js/window "popstate" process-query-str)

(html
  (head
    (meta :http-equiv "X-UA-Compatible" :content "IE=edge")
    (meta :name "description" :content "rot13")
    (meta :name "viewport" :content "width=device-width, initial-scale=1")
    ; TODO: do custom build locally instead of using getbootstrap.com/customize ?
    (link :rel "stylesheet" :href "bootstrap-custom.min.css")
    ; TODO: use something like https://github.com/noprompt/garden and
    ;       ouptut a (style :type "text/css") instead?
    (link :rel "stylesheet" :href "main.css")
    (link :rel "stylesheet" :href "https://fonts.googleapis.com/css?family=Cinzel:700")
    (link :rel "stylesheet" :href "https://cdnjs.cloudflare.com/ajax/libs/octicons/2.0.2/octicons.min.css")
    (link :rel "stylesheet" :href "https://cdnjs.cloudflare.com/ajax/libs/font-awesome/4.1.0/css/font-awesome.min.css")
    (link :rel "stylesheet" :href "https://cdnjs.cloudflare.com/ajax/libs/animate.css/3.1.0/animate.min.css")
    (title "rot13.io: Roman military grade encryption"))
  (body
    (div :id "root"
      (div :id "title" :class "animated fadeIn"
        (h1 "rot13.io")
        (h3 "Roman military grade encryption"))
      (div :id "main" :class "animated fadeInDownBig"
        input-el
        (button :class "btn btn-primary btn-lg btn-block"
                :disabled (cell= (or (= app-state :encrypting) (blank? in)))
                :click (fn [_] (do (@encrypt)
                                   (.pushState js/history nil (.-title js/document) @permalink)))
                (text (get {:encrypting "encrypting..."} app-state "rot13")))
        (div :class "animated btn-group btn-group-sm"
             :do-class (cell= {:fadeOut (= :awaiting-submit app-state) :fadeIn (not= :awaiting-submit app-state)}
                              )
          (a :class "btn btn-default" :id cp-button-id
             :data-clipboard-text (cell= in)
             :disabled (cell= (= false can-copy?))
             (span :class "octicon octicon-clippy") "copy")
          (a :class "btn btn-default" :id permalink-popover-id
             (span :class "octicon octicon-link") "link")
          (a :class "btn btn-default" :href (cell= mailto-link)
             (span :class "octicon octicon-mail") "email")
          (a :class "btn btn-default" :href (cell= tweet-link) :target "_blank"
             (span :class "fa fa-twitter") "tweet"))
        (div :toggle debug? (br) (pre :id "debug" (text "[~(name app-state)] ~{in}\n~{permalink}"))))
      (div :id "root_footer"))
    (div :id "footer" :class "animated fadeInUpBig"
      (div :class "pull-left"
        (a :href "https://en.wikipedia.org/wiki/Rot13" "what is this?"))
      (div :class "pull-right"
        (a :href "https://github.com/r0t13/rot13.io"
          (span :class "octicon octicon-mark-github"))))
    (script :src "https://cdnjs.cloudflare.com/ajax/libs/autosize.js/1.18.4/jquery.autosize.min.js")
    (script :src "https://cdnjs.cloudflare.com/ajax/libs/twitter-bootstrap/3.2.0/js/bootstrap.min.js")
    (script :src "ZeroClipboard.min.js")
    (script
;;{{
(function(i,s,o,g,r,a,m){i['GoogleAnalyticsObject']=r;i[r]=i[r]||function(){
(i[r].q=i[r].q||[]).push(arguments)},i[r].l=1*new Date();a=s.createElement(o),
m=s.getElementsByTagName(o)[0];a.async=1;a.src=g;m.parentNode.insertBefore(a,m)
})(window,document,'script','//www.google-analytics.com/analytics.js','ga');
ga('create', 'UA-46653299-1', 'rot13.io');
ga('require', 'linkid', 'linkid.js');
ga('send','pageview');
;;}}
     )))
